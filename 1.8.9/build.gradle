plugins {
    id 'java'
	id "org.quiltmc.loom"
	id 'ploceus'
}

group = maven_group
version = "${project.version}+${project.minecraft_18}"
archivesBaseName = "AxolotlClient-config"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
	maven {
		url = "https://moehreag.duckdns.org/maven/releases"
	}
	maven {
		name = "legacy-fabric"
		url = "https://repo.legacyfabric.net/repository/legacyfabric/"
	}
	mavenLocal()
	mavenCentral()
}

loom {
	mods {
		"axolotlclientconfig" {
			sourceSet("main")
		}
		"axolotlclientconfig-test" {
			sourceSet("test")
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_18}"
	mappings "net.ornithemc:feather:${project.mappings_18}"

	modRuntimeOnly "net.fabricmc:fabric-loader:${project.fabric_loader}"
	modCompileOnly "org.quiltmc:quilt-loader:${project.quilt_loader}"

	ploceus.dependOslModule("lifecycle-events", "0.5.1")
	ploceus.dependOslModule("entrypoints", "0.4.1")
	ploceus.dependOslModule("keybinds", "0.1.1")
	ploceus.dependOslModule("resource-loader", "0.2.4")
	ploceus.dependOslModule("core", "0.4.1")

	modCompileOnlyApi("com.terraformersmc:modmenu:0.1.0+mc1.8.9")

	implementation include(project(":common"))

	localRuntime("org.ow2.asm:asm-tree:9.4")

	modCompileOnly ("com.github.moehreag:legacy-lwjgl3:d27b6b3ce2") {
		exclude group: 'org.lwjgl', module: 'lwjgl-glfw'
		exclude group: 'org.lwjgl', module: 'lwjgl-openal'
		exclude group: 'org.lwjgl', module: 'lwjgl-opengl'
		exclude group: 'org.lwjgl', module: 'lwjgl'
		exclude group: 'net.fabricmc'
	}

	api include ("org.lwjgl:lwjgl-nanovg:3.3.2")
	include runtimeOnly ('org.lwjgl:lwjgl-nanovg:3.3.2:natives-linux')
	include runtimeOnly ('org.lwjgl:lwjgl-nanovg:3.3.2:natives-linux-arm64')
	include runtimeOnly ('org.lwjgl:lwjgl-nanovg:3.3.2:natives-windows')
	include runtimeOnly ('org.lwjgl:lwjgl-nanovg:3.3.2:natives-windows-arm64')
	include runtimeOnly ('org.lwjgl:lwjgl-nanovg:3.3.2:natives-macos')
	include runtimeOnly ('org.lwjgl:lwjgl-nanovg:3.3.2:natives-macos-arm64')

	api "org.slf4j:slf4j-simple:1.7.30"
	api "org.slf4j:slf4j-api:1.7.30"
}

processResources {
	inputs.property 'version', version

	filesMatching('fabric.mod.json') {
		expand "version": version
	}
}

runClient {
	classpath(sourceSets.test.runtimeClasspath)
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = archivesBaseName
			from components.java
		}
	}

	repositories {
		maven {
			name = "owlMaven"
			def repository = project.version.toString().contains("beta") || project.version.toString().contains("alpha") ? "snapshots" : "releases"
			url = "https://moehreag.duckdns.org/maven/"+repository
			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}
