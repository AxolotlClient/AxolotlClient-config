plugins {
    id 'java'
	id "org.quiltmc.loom" version "1.0.+"
}

group = maven_group
version = "${project.version}+${project.minecraft_119}"
archivesBaseName = "AxolotlClient-config"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
	maven {
		url = "https://moehreag.duckdns.org/maven/releases"
	}
	maven {
		url = "https://maven.terraformersmc.com/releases"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_119}"
	mappings "org.quiltmc:quilt-mappings:${project.mappings_119}:intermediary-v2"

	modImplementation "org.quiltmc:quilt-loader:${project.quilt_loader}"

	modApi include("org.quiltmc.qsl.core:lifecycle_events:${project.qsl}")
	modApi include("org.quiltmc.qsl.management:client_command:${project.qsl}")

	modApi("com.terraformersmc:modmenu:5.0.2")

	implementation include(project(":common"))
}

processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

runClient {
	classpath(sourceSets.test.runtimeClasspath)
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = archivesBaseName
			from components.java
		}
	}

	repositories {
		maven {
			name = "owlMaven"
			def repository = project.version.toString().contains("beta") || project.version.toString().contains("alpha") ? "snapshots" : "releases"
			url = "https://moehreag.duckdns.org/maven/"+repository
			credentials(PasswordCredentials)
			authentication {
				basic(BasicAuthentication)
			}
		}
	}
}
